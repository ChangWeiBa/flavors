apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zhouhaoh.flavors.sample"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "channel"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        beta {//测试环境
            dimension "channel"
            buildConfigField "int", "ENV_TYPE", "1"
            buildConfigField "String", "BASE_URL", "\"http://api.zhouhaoh.com/test/\""
            applicationIdSuffix ".beta"
            manifestPlaceholders = [
                    app_name   : "@string/app_name_beta",
                    app_icon   : "@mipmap/ic_launcher",
                    pgyer_appid: "pgyer_appid_01"
            ]
            resValue("string", "envTag", "beta环境")
            resValue("drawable", "logo", "@drawable/ic_launcher_foreground_beta")
        }
        product {//生产环境
            dimension "channel"
            buildConfigField "int", "ENV_TYPE", "2"
            buildConfigField "String", "BASE_URL", "\"http://api.zhouhaoh.com/product/\""
            manifestPlaceholders = [
                    app_name   : "@string/app_name",
                    app_icon   : "@drawable/ic_launcher",
                    pgyer_appid: "pgyer_appid_02"
            ]
            resValue("string", "envTag", "生产环境")
            resValue("drawable", "logo", "@drawable/ic_launcher_foreground_pro")
        }
        uat {//uat环境
            dimension "channel"
            buildConfigField "int", "ENV_TYPE", "3"
            buildConfigField "String", "BASE_URL", "\"http://api.zhouhaoh.com/uat/\""
            applicationIdSuffix ".uat"
            manifestPlaceholders = [
                    app_name   : "@string/app_name_uat",
                    app_icon   : "@mipmap/ic_launcher",
                    pgyer_appid: "pgyer_appid_03"
            ]
            resValue("string", "envTag", "uat环境")
            resValue("drawable", "logo", "@drawable/ic_launcher_foreground_uat")
        }
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name != "debug") {
            variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk")
        }
        variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
            apkData.outputFileName = "${variant.productFlavors[0].name}-v${variant.versionName}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
